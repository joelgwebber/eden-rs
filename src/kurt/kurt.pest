WHITESPACE = _{ " " | "\r" | "\n" }
COMMENT = _{
  ("-[" ~ (!"]-" ~ ANY)* ~ "]-") |
  ("-(" ~ (!")-" ~ ANY)* ~ ")-") |
  ("-{" ~ (!"}-" ~ ANY)* ~ "}-") |
  ("--" ~ (!NEWLINE ~ ANY)* ~ NEWLINE )
}

nil = { "nil" }

boolean = { "true" | "false" }

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
str_inner = @{ char* }
string = ${ "\"" ~ str_inner ~ "\"" }

id_char = { !(WHITESPACE | "[" | "]" | "{" | "}" | "(" | ")" | ":" | "\\" | "|" | ".") ~ ANY }
id = @{ id_char+ }

access = { non_access ~ ("." ~ non_access)+ }
prim = { number | string | boolean | nil }
list = { "[" ~ expr* ~ "]" }
pair = { expr ~ expr }
dict = { "{" ~ pair* ~ "}" }
apply = { "(" ~ expr* ~ ")" }
args = { id* ~ "|" }
block = { "(" ~ args ~ expr* ~ ")" }
quote = { ":" ~ expr }
unquote = { "\\" ~ expr }
non_access = { prim | id | list | dict | block | apply | quote | unquote }
expr = { access | non_access }

file = {
    SOI ~
    expr ~
    EOI
}
